# - Config file for @PROJECT_NAME@ and its component libraries
# It defines the following variables:
#
# @PROJECT_NAME@_INCLUDE_DIRS
# @PROJECT_NAME@_LIBRARIES
# @PROJECT_NAME@_INTERNAL_DEFINES - used by the test suite

# compute paths
get_filename_component(@PROJECT_NAME@_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

# version
include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-version.cmake)

@PACKAGE_INIT@

set_and_check(@PROJECT_NAME@_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(@PROJECT_NAME@_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")
get_filename_component(@PROJECT_NAME@_ROOT_DIR ${@PROJECT_NAME@_INCLUDE_DIR} PATH)

set(@PROJECT_NAME@_LIBRARIES)

add_library(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE IMPORTED)

find_package(
    AMDDeviceLibs
    REQUIRED
    CONFIG
    HINTS
    @AMDDeviceLibs_DIR@
    @rocm_version_DIR@
    PATHS
    @AMDDeviceLibs_DIR@
    @rocm_version_DIR@)

find_package(
    amd_comgr
    @amd_comgr_VERSION@
    EXACT
    REQUIRED
    CONFIG
    HINTS
    @amd_comgr_DIR@
    @rocm_version_DIR@
    PATHS
    @amd_comgr_DIR@
    @rocm_version_DIR@)

find_package(
    hsa-runtime64
    @hsa-runtime64_VERSION@
    EXACT
    REQUIRED
    CONFIG
    HINTS
    @hsa-runtime64_DIR@
    @rocm_version_DIR@
    PATHS
    @hsa-runtime64_DIR@
    @rocm_version_DIR@)

find_package(
    hip
    @hip_VERSION@
    EXACT
    REQUIRED
    CONFIG
    HINTS
    @hip_DIR@
    @rocm_version_DIR@
    PATHS
    @hip_DIR@
    @rocm_version_DIR@)

include("${@PROJECT_NAME@_CMAKE_DIR}/@PROJECT_NAME@-library-targets.cmake")

# Library dependencies
foreach(TARG @PROJECT_BUILD_TARGETS@)
    set(TARG @PROJECT_NAME@-${TARG})
    if(NOT @PROJECT_NAME@_FIND_COMPONENTS)
        list(APPEND @PROJECT_NAME@_LIBRARIES @PROJECT_NAME@::${TARG})
        target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@
                              INTERFACE @PROJECT_NAME@::${TARG})
    endif()
endforeach()

if(@PROJECT_NAME@_FIND_COMPONENTS)
    foreach(COMP ${@PROJECT_NAME@_FIND_COMPONENTS})
        set(TARG @PROJECT_NAME@::@PROJECT_NAME@-${COMP})
        if(TARGET ${TARG})
            set(@PROJECT_NAME@_${COMP}_FOUND 1)
            list(APPEND @PROJECT_NAME@_LIBRARIES ${TARG})
            target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE ${TARG})
        else()
            set(@PROJECT_NAME@_${COMP}_FOUND 0)
        endif()
    endforeach()
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    @PROJECT_NAME@
    FOUND_VAR @PROJECT_NAME@_FOUND
    REQUIRED_VARS @PROJECT_NAME@_ROOT_DIR @PROJECT_NAME@_INCLUDE_DIR
                  @PROJECT_NAME@_LIBRARIES
    VERSION_VAR @PROJECT_NAME@_VERSION
    HANDLE_COMPONENTS)
